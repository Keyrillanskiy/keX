apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

group = "com.github.keyrillanskiy"
version = KeXVersion

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
        }
    }
    developers {
        developer {
            id "keyrillanskiy"
            name "Kirill Chirkin"
        }
    }
    scm {
        url bintrayUrl
    }
}

publishing {
    publications {
        KotlinPublication(MavenPublication) {
            from components.java
            groupId 'com.github.keyrillanskiy'
            artifactId 'kotlin'
            version KeXVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'KeX-kotlin')
                root.appendNode('description', 'KeX - keyrillanskiy\'s extensions for kotlin and android. The goal of KeX is to store the most useful extensions in one library.')
                root.appendNode('url', bintrayUrl)
                root.children().last() + pomConfig

                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    def propertiesFile = file("${rootDir}/local.properties")
    if (propertiesFile.exists()) {
        properties.load(new FileInputStream(propertiesFile))
    }

    user = properties['bintray.user']
    key = properties['bintray.api_key']

    publications = ['KotlinPublication']
    publish = true

    pkg {
        repo = 'maven'
        name = 'KeX-kotlin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Keyrillanskiy/keX'
        dryRun = false

        version {
            name = KeXVersion
            desc = 'alpha'
            released = new Date()
            vcsTag = KeXVersion
        }
    }
}