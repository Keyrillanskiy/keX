apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName = KeXVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-dev"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
        }
    }
    developers {
        developer {
            id "keyrillanskiy"
            name "Kirill Chirkin"
        }
    }
    scm {
        url bintrayUrl
    }
}

publishing {
    publications {
        AndroidPublication(MavenPublication) {
            artifact("$buildDir/outputs/aar/android-release.aar")
            groupId 'com.github.keyrillanskiy'
            artifactId 'android'
            version KeXVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'KeX-android')
                root.appendNode('description', 'KeX - keyrillanskiy\'s extensions for kotlin and android. The goal of KeX is to store the most useful extensions in one library.')
                root.appendNode('url', bintrayUrl)
                root.children().last() + pomConfig

                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    def propertiesFile = file("${rootDir}/local.properties")
    if (propertiesFile.exists()) {
        properties.load(new FileInputStream(propertiesFile))
    }

    user = properties['bintray.user']
    key = properties['bintray.api_key']

    publications = ['AndroidPublication']
    publish = true
//    override = true

    pkg {
        repo = 'maven'
        name = 'KeX-android'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Keyrillanskiy/keX'
        dryRun = false
        publicDownloadNumbers = true

        version {
            name = KeXVersion
            desc = 'alpha'
            released = new Date()
            vcsTag = KeXVersion
        }
    }
}